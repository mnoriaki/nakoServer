!「nadesiko3-websocket」を取り込む。
!「plugin_express」を取り込む。
WEB_PORT=8080
WS_PORT=5001

#--- WebSocket サーバ ---
WSサーバ起動成功した時には
  「● WebSocketサーバ(ECHOサーバ)を起動しました」と表示。
  自分IPアドレス取得して反復
    「- ws://{対象}:{WS_PORT}」を表示。
  ここまで
ここまで
WSサーバ起動失敗した時には
  「WebSocketサーバの起動に失敗しました」と表示。
ここまで
WSサーバ接続した時には
  IP=対象["connection"]["remoteAddress"]
  port=対象["connection"]["remotePort"]
  「クライアント{IP}:{port}が接続しました」と表示。
  「名前を教えてね」をWSサーバ個別送信。
ここまで

WSサーバ受信時には
  「受信[{WSサーバ相手}]:{対象}」と表示。
   (3の乱数)で条件分岐
      0ならば、
        前世=「猫」
      ここまで
      1ならば、
        前世=「カエル」
      ここまで
      2ならば、
        前世=「和尚」
      ここまで
  ここまで
   (3の乱数)で条件分岐
      0ならば、
        場所=「京都」
      ここまで
      1ならば、
        場所=「アメリカ」
      ここまで
      2ならば、
        場所=「インド」
      ここまで
  ここまで
  「{対象}さんの前世は{場所}の{前世}でした」をWSサーバ個別送信
ここまで
app=WS_PORTでWSサーバ起動。

#--- Webサーバ ---
WEB_PORTでWEBサーバ起動した時には
　「下記のアドレスにWEBブラウザでアクセスしてください。」と表示。
  自分IPアドレス取得して反復
    「- http://{対象}:{WEB_PORT}」を表示。
  ここまで
 「/」を「data1」へWEBサーバ静的パス指定。
ここまで。